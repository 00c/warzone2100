//
// Multi-player script for upto 8 players.
//

public	STRUCTURESTAT	command;			
public	STRUCTURESTAT	factory;			
public	STRUCTURESTAT	wall;			
public	STRUCTURESTAT	cornerWall;		
public	STRUCTURESTAT	oilDerrick;		
public	STRUCTURESTAT	powerGen;		
public	STRUCTURESTAT	research;
public	STRUCTURESTAT	commandrelay;
public 	STRUCTURESTAT	cybfac;
public 	STRUCTURESTAT	vtolfac;

public	int				numCleanTech,	numBaseTech,	numDefTech;
public	RESEARCHSTAT	cleanTech[32],	baseTech[32],	defTech[64];

public	int				numCleanRes,	numBaseRes,		numDefRes;
public	RESEARCHSTAT	cleanRes[10],	baseRes[10],	defRes[10];		

//	Other Stuff
private	INT				count;
private INT				playnum;
private	BOOL			gamenotwon;

// Base Under Attack Stuff 
private	STRUCTURE		hitStruc;
private	BASEOBJ			attackerObj;
private	int				t;
public	SOUND			attackSnd1;	

public	INTMESSAGE		endMsg, winMsg;

// /////////////////////////////////////////////////////////////////

trigger	endConditions(every, 100);	

// /////////////////////////////////////////////////////////////////
//this event is called once the game has initialised itself

event initialisedEvent(CALL_GAMEINIT)
{
	playnum=0;
	while (playnum < 8)
	{
		if(multiPlayerBaseType == CAMP_CLEAN)
		{
			setPowerLevel(750,playnum);	
			count = 0;								
			while (count < numCleanTech)
			{
				completeResearch(cleanTech[count], playnum);
				count = count +1;
			}
			
			count = 0;
			while (count < numCleanRes)
			{
				enableResearch(cleanRes[count], playnum);
				count = count +1;
			}
		}
		else if(multiPlayerBaseType == CAMP_BASE)
		{
			setPowerLevel(1000,playnum);	
			count = 0;								
			while (count < numBaseTech)
			{
				completeResearch(baseTech[count], playnum);
				count = count +1;
			}
			count = 0;								
			while (count < numBaseRes)
			{
				enableResearch(baseRes[count], playnum);
				count = count +1;
			}
		}
		else
		{	
			setPowerLevel(2000,playnum);		
			count = 0;								
			while (count < numDefTech)
			{
				completeResearch(defTech[count], playnum);
				count = count +1;
			}
			count = 0;								
			while (count < numDefRes)
			{
				enableResearch(defRes[count], playnum);
				count = count +1;
			}
		}
	
		playnum = playnum + 1;		
	}

}


event initialisedEvent2(CALL_GAMEINIT)
{
	//set up the reticule buttons
	addReticuleButton(OPTIONS);
	addReticuleButton(CANCEL);
	addReticuleButton(BUILD);
	addReticuleButton(MANUFACTURE);
	addReticuleButton(RESEARCH);
	addReticuleButton(INTELMAP);
	addReticuleButton(DESIGN);

	playnum=0;
	while (playnum < 8)
	{
		enableStructure(command	,	playnum);		//make structures available to build
		enableStructure(factory,	playnum);
		enableStructure(oilDerrick,	playnum);
		enableStructure(powerGen,	playnum);
		enableStructure(research,	playnum);
		
		setStructureLimits(factory,	5, playnum);	// set structure limits
		setStructureLimits(powerGen,	5, playnum);
		setStructureLimits(research,	5, playnum);
		setStructureLimits(command,	1, playnum);
		setStructureLimits(commandrelay,1, playnum);
		setStructureLimits(cybfac,	5, playnum);
		setStructureLimits(vtolfac,	5, playnum);

		playnum = playnum+1;
	}
	applyLimitSet();	// set limit options
}


// /////////////////////////////////////////////////////////////////
event checkEndConditions (endConditions)
{
	// Losing Conditions
	if ((not anyDroidsLeft(selectedPlayer)) and (not anyFactoriesLeft(selectedPlayer)))
	{
		gameOverMessage(endMsg, MISS_MSG, 0, FALSE);
		setEventTrigger(checkEndConditions, inactive);
	}
	
	// Winning Conditions
	playnum=0;
	gamenotwon = FALSE;

	////////////////
	// Campaign Game
	if(multiPlayerGameType == CAMPAIGN)
	{
		// check humans are still about
		while (playnum < multiPlayerMaxPlayers)
		{
			if (playnum != selectedPlayer)
			{
				if(anyDroidsLeft(playnum) or anyFactoriesLeft(playnum) )
				{
					gamenotwon = TRUE;
				}
			}
			playnum = playnum + 1;			
		}
		
		//check computer/babas are wiped out completely
		while(playnum<8)
		{
			if(not isHumanPlayer(playnum))
			{
				if(anyDroidsLeft(playnum) or anyStructButWallsLeft(playnum))
				{
					gamenotwon = TRUE;
				}
			}
			playnum = playnum + 1;			
		}			
	}
	
	if(gamenotwon == FALSE)
	{
		gameOverMessage(winMsg, MISS_MSG, 0, TRUE);
		setEventTrigger(checkEndConditions, inactive);
	}	
}

// /////////////////////////////////////////////////////////////////
/* Base Under Attack */
event baseHit(CALL_STRUCT_ATTACKED, selectedPlayer, ref hitStruc, ref attackerObj)
{
	if (t >= 10)
	{
		t=0;
		if (hitStruc != NULLOBJECT)
		{
			playSoundPos(attackSnd1, selectedPlayer, hitStruc.x, hitStruc.y, hitStruc.z);	//show position if still alive
		}
		else
		{
			playSound(attackSnd1, selectedPlayer);
		}
	}
}

event everySec(every, 10)
{
	t=t+1;
}

//go to where the structure being attacked is on CTRL B
event seeBaseHit(CALL_MISSION_END)
{
	if (hitStruc!=NULLOBJECT)
	{
		centreView(hitStruc);
		t=0;							//flag known about!
	}
}
